;
;  Copyright (c) 2017, Texas Instruments Incorporated
;  All rights reserved.
;
;  Redistribution and use in source and binary forms, with or without
;  modification, are permitted provided that the following conditions
;  are met:
;
;  *  Redistributions of source code must retain the above copyright
;     notice, this list of conditions and the following disclaimer.
;
;  *  Redistributions in binary form must reproduce the above copyright
;     notice, this list of conditions and the following disclaimer in the
;     documentation and/or other materials provided with the distribution.
;
;  *  Neither the name of Texas Instruments Incorporated nor the names of
;     its contributors may be used to endorse or promote products derived
;     from this software without specific prior written permission.
;
;  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;  AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO,
;  THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
;  PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR
;  CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
;  EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
;  PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS;
;  OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
;  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
;  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE,
;  EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;
;
; ======== SysCall_asm.sv7M ========
;

        .cdecls C,NOLIST,"SysCall_asm.h"

        .thumb

        .global _kernel_wrapper_funcs_begin
        .global _kernel_wrapper_funcs_end
        .global ti_sysbios_family_arm_v7m_SysCall_enterPrivModeI__I
        .global ti_sysbios_family_arm_v7m_SysCall_enterUnprivModeI__I
        .global ti_sysbios_family_arm_v7m_SysCall_restorePrivModeI__I
        .global ti_sysbios_family_arm_v7m_SysCall_svcHandler__I

ti_sysbios_family_arm_v7m_SysCall_Module__state__V .tag ti_sysbios_family_arm_v7m_SysCall_Module_State
ti_sysbios_knl_Task_Module__state__V .tag ti_sysbios_knl_Task_Module_State

        .sect ".text:ti_sysbios_family_arm_v7m_SysCall_svcHandler__I"
ti_sysbios_family_arm_v7m_SysCall_svcHandler__I:
        .asmfunc
        ; SVC interrupt's priority is 0 (highest) by default and is not
        ; changed by BIOS. Therefore, an SVC interrupt cannot be nested
        ; and we don't have to worry about stack issues.
        tst     lr, #0x4
        ite     eq
        mrseq   r3, msp
        mrsne   r3, psp
        ldr     r0, [r3, #24]   ; read stacked PC

        ; Check if PC within kernel wrapper function code region
        ; If not, return without doing anything
        ldr     r1, kernel_wrapper_begin
        cmp     r0, r1
        it      LO
        blo     validationFailed
        ldr     r1, kernel_wrapper_end
        cmp     r0, r1
        it      HI
        bhi     validationFailed

        ldrb    r0, [r0, #-2]   ; read svc num
        lsl     r0, r0, #2
        ldr     r1, sysCallTable
        ldr     r1, [r1]
        ldr     r0, [r1, r0]
        push    {r0, lr}
        blx     r0              ; jump to svc function
        pop     {r0, lr}
        bx      lr
validationFailed:
        movw    r1, ti_sysbios_family_arm_v7m_SysCall_requestFailedFunc__C
        movt    r1, ti_sysbios_family_arm_v7m_SysCall_requestFailedFunc__C
        ldr     r1, [r1]
        ldrb    r0, [r0, #-2]   ; read svc num
        push    {r0, lr}
        blx     r1
        pop     {r0, lr}
        bx      lr
sysCallTable:
        .word   ti_sysbios_family_arm_v7m_SysCall_Module__state__V.sysCallTable
kernel_wrapper_begin:
        .word   _kernel_wrapper_funcs_begin
kernel_wrapper_end:
        .word   _kernel_wrapper_funcs_end
        .endasmfunc

        .sect ".text:ti_sysbios_family_arm_v7m_SysCall_enterPrivModeI__I"
ti_sysbios_family_arm_v7m_SysCall_enterPrivModeI__I:
        .asmfunc
        mrs     r0, control
        bic     r1, r0, #1
        msr     control, r1
        isb
        bx      lr
        .endasmfunc

        .sect ".text:ti_sysbios_family_arm_v7m_SysCall_enterUnprivModeI__I"
ti_sysbios_family_arm_v7m_SysCall_enterUnprivModeI__I:
        .asmfunc
        mrs     r0, control
        orr     r1, r0, #1
        msr     control, r1
        isb
        bx      lr
        .endasmfunc

        .sect ".text:ti_sysbios_family_arm_v7m_SysCall_restorePrivModeI__I"
ti_sysbios_family_arm_v7m_SysCall_restorePrivModeI__I:
        .asmfunc
        ldr     r0, curTaskPrivileged
        ldrb    r0, [r0]
        eor     r0, r0, #1
        mrs     r1, control
        bic     r1, r1, #1
        orr     r1, r0, r0
        msr     control, r1
        isb
        bx      lr
curTaskPrivileged:
        .word   ti_sysbios_knl_Task_Module__state__V.curTaskPrivileged
        .endasmfunc

        .end
